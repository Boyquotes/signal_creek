== consoles ==

{
    - consoles_end:
        -> consoles_end2
    - nourplaysfantasygame:
        -> consoles_end
    - noursuggestion:
        -> nourplaysfantasygame
    - consoles && consoles2:
        -> consoles_visited
    - consoles:
        -> consoles2
    // - else: 
    //     -> consoles
}

It's a new edition GameStage console, running a demo of the hit game FANTASY GAME. Nick seems to be excited.

NICK: "Fantasy Game? Here? In its pristine, original condition?"

NOUR: "Looks like it still runs just fine."

NICK: "Man, I can't believe it's here! They discontinued this edition years ago!"

    * [Ask if he wants to play]
    
        NOUR: "You wanna give it a go?"
        
        NICK: "I—!"
        
        Nick falters. A somber expression passes his face. 
        
        NICK: "Um...no. This stuff's for babies so..."
        
        NICK: "I probably shouldn't."
        
    * [Leave the video game console]
    
        NOUR: "We should look for something that could help us."
        
        Nick nods glumly. He looks at the game one more time, and then he sighs.

- NOUR: "Hey, are you alright?"

NICK: "I'm fine. Let's just go already."

NOUR: "Whatever you say..."

NOUR: <i>Weird. He seemed like he wanted to talk about this game, but I guess something about it is bothering him.</i>

NOUR: <i>Maybe he'll want to talk about it later...</i>

    -> END

== consoles2 ==

The soft 8-bit boops of FANTASY GAME play, but Nick only stares at the screen. 

NOUR: <i>Why isn't he saying anything? Maybe I have to start first...</i>

NOUR: "So, Fantasy Game. Seems like you knew a lot about it, Nick."

NICK: "Yeah. I don't know. Not really."

MS. SUWAN: "That was quite literally incomprehensible."

NICK: "I meant to say no! Just get off my case already, ok?"

MS. SUWAN: "No need to be defensive. It wasn't a personal attack."

NICK: "Whatever..."

Nick huffs and turns away from the game. 

NOUR: <i>Nick doesn't seem to want to talk about this yet. I'll come back here later when he's ready.</i>

-> END

== consoles_visited ==
NOUR: <i>Nick doesn't seem to want to talk about this yet. I'll come back here later when he's ready.</i>

-> END

== nourplaysfantasygame ==

Nick remains silent and turned away from the console. 

    * [Pick up the controller and play]

- NOUR: "Even if you don't want to play, <i>I</i> still want to."

NICK: "..."

NOUR: "Here goes nothing..."

A tiny character pops up in the middle of the screen. Rows of weapons and a million character stats overwhelm the space. 

Nick leans closer but still says nothing. 

    * "Have something to say?"
        -> nicktalksaboutfantasygame
    
    * "Having fun watching?" 
        -> nicktalksaboutfantasygame
    
    * "Want to join me?"

- NICK: "... It's alright, it's a single player game. I can watch."

NOUR: "You sure? I just started, you can hop in if you want?"

NICK: "No, yeah, it's fine. Can I... can I watch?"

Nour moves over slightly to give Nick the space to watch, nodding with a slight smile. 
    -> nicktalksaboutfantasygame

== nicktalksaboutfantasygame ==

NICK: "..."

NICK: "Are you sure you want to use a bow with a strength character? It's not really an optimal build."

NICK: "You won't get a lot of use out of your character's strength unless you use, like, a sword or something."

NOUR: "I didn't know, thanks. I guess I'll switch over to another weapon."
    
Nick nods quietly, content.
    
NOUR: "Oh... but I like the bow. It looks nice."
    
Nick purses his lips but doesn't seem frustrated. He seems to be thinking.
        
NICK: "Ok, ok. We can work with that..."
        
NICK: "You can try and change your stats a bit later in the game, but for early game, you could just restart and make a new character to match the play style you like?"
        
NICK: "If you like the bow, you should have a good time playing it from the beginning. Or else this run might really suck."
        
NOUR: "...I think I'll do that, then."

Nick stares intently at the game menu restart with a new character, his leg bouncing up and down.

NOUR: "Ok. First level. Shouldn't be too hard...right, Nick?"

Nick shrugs, but his eyes are glued to the screen.

NOUR: <i>He's really getting into this. Why did he act like he hated this game before, anyway?</i>

NICK: "WATCH OUT!" 

NOUR: "SHOOT!"

The character barely dodges a hidden enemy. 

NOUR: "...<i>NICK</i>, you SCARED me."

Nick pauses for a second and then grins sheepishly.

NICK: "Crap, sorry..."

NICK: "You, uh... you almost stepped into a trap that would've probably insta kill you."

NICK "You see those red glowing orbs? Fire trap. It <i>blows</i>... <i>literally</i>."

He laughs at his own joke. Ms. Suwan turns away, but there's a small smile creeping up her face.

NOUR: "Nice pun."

NOUR: "You know, maybe I should stop playing. It's probably not fun to watch me die every five seconds."

NICK: "No! It's fine. This is technically not <i>supposed</i> to be an easy game..."

NICK: "I should just shut up and let you play. It's not like I'm being helpful by shouting in your ear."

    *"But you seem really knowledgeable about the game."
    
        NOUR: "You know that's actually a good thing for a noob like me, right?"
        
        NICK: "I might know a thing or two..."
    
        He blushes a little, kicking his foot and unable to hide a proud smile.
        
    *"No, don't shut up! I'm only surviving because of you!" 
        
        NOUR: "If you don't keep telling me all your tips, I'm gonna die way more. That would be the worst."
        
        Nick beams before clearing his throat, slightly embarassed.
        
- NICK: "Yeah, Fantasy Game... it's one of my favorites, if not <i>the</i> favorite game I have..."

NICK: "The only problem is that the tutorial is kind of garbage. It assumes the player already knows a bunch of stuff, which isn't true."

NOUR: "Do you play it a lot? Games are cool—you can totally make gaming your cool thing."

Nick's face suddenly falls, and he goes quiet for a moment, glaring at the ground.

NICK: "I don't... actually own the game. I watch a lot of videos about it and watch <i>others</i> play it but..."

    * [Stay quiet and wait]
    
    * [Gently urge him to speak]
        NOUR: "It's alright, you can talk to me about it..."

- Nick seems to fumble for the correct words before shrugging, seemingly resigned. 

When he looks back up at Nour his eyes are glimmering—not quite crying but close to it.

NICK: "My mom wouldn't let me get the game and play it because my grades were shit." 

NICK: "She said I could get it if I fixed them but it's... it's fucking <i>hard</i>. I'm too dumb to do anything right." 

NOUR: "You're not dumb, Nick. You know so much about this game, probably far more than you've even let on in this short amount of time."

NOUR: "Right, Ms. Suwan?"

Nick glances at Ms. Suwan nervously. She clears her throat.

MS. SUWAN: "Intelligence comes in all forms. Just because society values one over the other doesn't make it any less valuable." 

NICK: "But it's not the <i>same</i>! It's a dumb video game, not like... not <i>math</i> or <i>science</i> or shit."

NICK: "I don't have the time to focus on school, and even when I <i>do</i> it's fucking <i>hard</i>."

NICK: "It's like there's a... a brick wall just in front of <i>everything</i>."

    * "But you love this game. You've dedicated so much time to it."
        
        NOUR: "You're not useless or dumb for liking something a lot. To memorize everything about anything takes a good amount of smarts."
        
        Nick's eyes widen.
        
        NICK: "You mean that?"
        
        NOUR: "Yeah, I do."
    
        He curls his fist tightly, but after a moment, tears start to leak out of his eyes even as he tries to force them down.
        
        NICK: "No one's ever...<i>Everyone</i> thinks I'm dumb. I've been useless my entire life..."
        
        NICK: "But yeah...I love this game. It means a lot to me."
        
        NICK: "I've always wanted to dedicate more time to it."
    
    * "I'm sorry that it's been so hard for you... but acting out isn't going to make it better.["] If you keep doing this, then you'll never be able to truly get into the thing you love."
    
        NICK: "So what should I do?"
        
        NICK: "It's not that easy. My life is a <i>mess</i>. I don't know what else to do."

- Nour looks over at Nick's wrist, and suddenly the realization seems to dawn on him. He looks at the bracelet.

NOUR: "You don't have to rebel against everything to try and regain some kind of control over your life."

NOUR: "You can focus as much as you can on school, and then if you do well, you can dedicate the rest of your life to games..."

NOUR: "Playing them, making them, whatever you want."

NICK: "... You can make games?"

NOUR: "Yeah! Who do you think made your favorite one?"

Nick thinks for a moment, before looking over at Ms. Suwan, then at Nour. Then, he slowly slides the bracelet off, holding it out to Nour. 

NICK: "... Here. Can you put it back for me? I... I don't need it anymore."

Nour smiles and takes the bracelet.

NOUR: "Sure thing, Nick."
&LIGHT_Nick2
&MOV_RINA_TOPICSPOT
    -> light_on
// -> END

=== consoles_end ===

The bow-user character from the last game played stands idle on the screen.

NICK: "Next time, we should try to play a tank character! Defense is the best offense, right?"

MS. SUWAN: "It's the opposite, actually."

NICK: "Oh..."

MS. SUWAN: "But I'm not the expert, remember? That's you."

MS. SUWAN: "So perhaps, defense <i>is</i> the best offense."

NICK: "Maybe! I guess the only way to find out would be to play..."

-> END

=== consoles_end2 ===

NOUR: <i>I've already looked at this. Maybe I should try looking at something or talking to someone else.</i>

-> END