== bookshelf ==

    It's a tall bookshelf, stacked with all kinds of instructional guides, exam prep workbooks, and how-to books... for those interested in <i>cool</i> new hobbies!
    
    {
        - noursuggestion && nicktalksaboutfantasygame:
            -> END
            
        - spraycans1:
            -> noursuggestion
    }
    
    This might come in handy.

    -> END

== bookshelfback == 

    -> bookshelf
    
== bookshelfside ==

    ->bookshelf

    
== noursuggestion ==

    * "Ugh... This makes me think about high school."
    
        NOUR: "My parents expected me to get the best possible exam scores I could. I guess if you're the only child, you have to be everything."
        
        NICK: "Screw that. Tests are meaningless... Who's gonna care about how good I am at math when I'm thirty?"
        
        MS. SUWAN: "Your employers, your clients, the government..."
        
        MS. SUWAN: "You're going to have to take tests for the rest of your life. Get used to it."
        
        NICK: "You're really <i>testing</i> my patience, ma'am."
        
        -> noursuggestion
        
    
    * "Look at this, Nick... <i>How to Draw Manga Characters</i>"
    
        -> nickargument
    
    * "There's also... <i>Get Ready for LSATs</i>."
    
        Nour looks over at Ms. Suwan.
    
        MS. SUWAN: "I didn't have that particular book, though I did use study aids."
        
        MS. SUWAN: "Taking that extra step ensures that you excel at what you do."
        
        -> noursuggestion
    
    + I'm done looking.
    
    -> END
        
== nickargument ==

    NICK: "Ah, cool, I guess."
    
        * "Are you interested?["] It might be cool to try learning something new. That's what got me into photography."
        
        * "People who can draw are pretty cool.["] You never know, you might end up liking it. That was my experience with photography."
    
    - NICK: "Not my cup of tea, man. To each their own."
    
    NOUR: "What about..."
    
        * "Fashion Design 101?"
        
        * "How it's made- Video Games?"
        
        * "JavaScript for Poopyheads?"
        
    - NICK: "I literally <i>don't care</i>. I don't want to study anything."
    
    MS. SUWAN: "You're going to end up a deadbeat if you don't change that mindset of yours."
    
        * "That's a bit harsh, Ms. Suwan..."
        
            MS. SUWAN: "Listen, if this kid's parents aren't giving him the truth, someone has to. I had to learn the hard way."
        
        * "Nick, people will think you're cool if you know how to do something. Isn't that what you want?"
        
            NICK: "I don't have to know how to do something that requires me to <i>read</i> for fun."
            
            NICK: "I just... I don't know, it's a waste of time."
            
            MS. SUWAN: "As if you're not wasting your time now, being a delinquent teenager?"
        
    - NICK: "You're old and boring. Study <i>this</i>, responsiblility <i>that</i>, whatever. Do you even have friends?"
    
    MS. SUWAN: "I'm not a <i>child</i> who plays games with their friends. That's not how the real world works-"
    
        * "Making the real world seem bleak won't help anyone.["] You can have fun practicing your skills, and it may even lead you somewhere in life."
        
        * "You can have fun and work hard at the same time...["] Not everything has to be miserable, not even work."
    
    - NOUR: "Look, whether I make money from photography or not, I'll always love doing it."
    
    NICK: "Look at you. Aren't you so lucky, you have something you're good at. Not everyone can be good at stuff."
    
        * "I still had to practice, I wasn't born with the ability to take pictures.["] It's okay to put time into something before you're good at it."
        
        * "How do you know I'm good at it?["] For all you know, I could suck at taking pictures."
        
            NOUR: "It doesn't matter, because I actually put the time into it, and at least I <i>know</i> things."
    
    - MS. SUWAN: "Stop expecting things to just be placed in your lap."
    
    NICK: "I'm so TIRED of people saying I'm lazy. You <i>DON'T</i> know my story! People expect me to do everything!"
    
    * "We're just trying to help..."
    
    * "You don't even want to look at one of these books?"
    
    * "Tell us your story, then. Tell us your side of things."
    
    - NICK: "I DON'T WANT YOUR HELP."
    
    Nick averts Nour's gaze, visibly restraining himself.
    
    * ["Why do these books make you so upset?"]
    
        NOUR: "Why do-"
    
    * ["What do you want us to do for you?"]
    
        NOUR: "What do you-"
        
    * ["I didn't mean to upset you..."]
    
        NOUR: "I didn't mean-"
        
    - NICK: "SCREW YOU!"
    
    Nick throws his hand in front of Nour and Ms. Suwan, boasting a nice middle finger.
    
    On Nick's wrist, Nour sees a black bracelet with metallic studs.
    
    //NOUR: <i>Wasn't that bracelet in TopicSpot...?</i>
    
    NOUR: <i>Wait. I don't recognize that bracelet on him?</i>
    
    NOUR: <i>Is that a tag on the end of it...?</i>
    
    MS. SUWAN: "...Well, isn't that nice."
    
        * "Ms. Suwan, do you recognize that bracelet?"
        
            MS. SUWAN: "Not on Nick."
        
        * "Nick, where'd you get that bracelet?"
        
        * Say nothing.
    
    - Nick quickly retracts his hand, hiding it behind his back.
    
    MS. SUWAN: "Suspicious. If you had gotten that bracelet through innocent means, why hide your hand now?"
    
        * "Did you steal the bracelet, Nick?"
        
        * "You should probably put that back, Nick."
    
    - NICK: "I'm not talking to you two anymore. All you do is talk down to me."
    
        * "Please, we just want you to do the right thing..."
        
        * "All you need to do is return the bracelet..."
    
    - Nick shakes his head.
    
    &LIGHT_Nick1
    
    BEEP ELEVATOR LIGHT SFX
    
    -> light_on
    
    