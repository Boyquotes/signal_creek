== musicstand ==

    It's a music stand. Different vintage records are stacked on top of it. 

    {
        - musicstand4:
            -> explored_everything
            
        - chadbrodykristy3:
            -> musicstand4
            
        - musicstand2:
            -> musicstand2_repeat
            
        - chadbrodykristy1 && musicstand1 && cafetables_getsuwantotalk:
            -> musicstand2
            
        - chadbrodykristy1 && musicstand1:
            -> musicstand1_repeat
            
        - chadbrodykristy1:
            -> musicstand1
    }
    
    -> END


== musicstand1 ==
//better onboarding from CBK
    
    One record in particular catches Nour's attention. 
    
    NOUR: "Ms. Suwan! You mentioned going to rock shows in the past, right?"
    
    MS. SUWAN: "... Yes, why?"
    
    // NOUR WALKS FORWARD AND TAKES RECORD FROM THE MUSIC STAND, THEN TURNS AROUND TOWARDS MS SUWAN, HOLDING IT UP.
    
    NOUR: "Have you heard of this album, then?"
    
    Nour raises the record of <i>Actually, I Do Mind</i> up to show Ms. Suwan. 
    
    NOUR: "I used to listen to Paradise all the time when I was younger."
    
    MS. SUWAN: "...Yes. I remember that one. Good album."
    
    NICK: "You listen to <i>rock</i>?" // (SHOCKED)
    
    MS. SUWAN: "Yes. Well... used to."
    
    &PORTRAIT_nour_nourshocked
    
    NOUR: "Oh... that's, wow!"
    
    MS. SUWAN: "Why are you looking at me like that?" // (FROWNING)
    
    NOUR: "Uh, well..."
        
        * "I didn't expect you to listen to a rock band."
            
            NOUR: "You don't really seem like the type - no offense." 
            
            MS. SUWAN: "I was very different back then."
            
            NOUR: "Do you listen to them now?"
            
            MS. SUWAN: "No. It was a distraction, so I stopped."
            
            NOUR: <i>She looks a little uncomfortable.</i>
        
        * "It's nothing. I wasn't going to say anything—"
        
            MS. SUWAN: "You were going to say that I don't seem like the type, right?"
    
            NOUR: "Um... yeah. Sorry." // (EMBARASSED)
            
            MS. SUWAN: "Well, I don't anymore, so you aren't entirely incorrect. It was a distraction, so I stopped."
            
            NOUR: <i>She looks a little uncomfortable.</i>
    
-   NOUR: "I guess you probably were a student while Paradise came out with this, right?"
    
    MS. SUWAN: "Right. During school, me and my friends used to..."
    
    MS. SUWAN: "..." // (FROWNING)
        * "Are you okay?"
            
            MS. SUWAN: "I'm fine. Paradise just reminds me of hard times. That's all."
            
            NICK: "Kind of ironic, given the name." // (HAPPY)
            
            MS. SUWAN: "..." // (ANGRY)
            
            NICK: "...Sorry." // (POUTING)

        * "You went to Seton Hall University, right?"
    
            MS. SUWAN: "Right. You asked me this during our interview, remember?"
    
            NOUR: "Yeah... we didn't go into it that much, though. Did you like it?"
    
            MS. SUWAN: "... It was challenging, but my education there got me where I needed to be."
            
        * "What was it that you and your friends used to do?"
            
            MS. SUWAN: "..."
    
    - NOUR: "Did you listen to this with your friends?"
    
    MS. SUWAN: "...Yes. After moving to New Jersey from Thailand, I came to law school."
    
    MS. SUWAN: "Since we all lived in the same dorm floor, we became close." 
    
    MS. SUWAN: "We used to play Paradise while we studied together."
    
    MS. SUWAN: "I think I owe Paradise my first real friendships." // (SAD)
    
    NICK: "Rocking out led to your friendships? That's so cool..." // (EXCITED)
    
    MS. SUWAN: "But that was a different time. Things change."
    
    NICK: "How 'bout we rock out right now then? For old time's sake?" // (EXCITED)
    
    MS. SUWAN: "I don't think that would be a good use of our time right now."
    
        * "Do you mind if I play this album? I really miss it."
            
        * "Maybe we could play it for Nick?"
        
            NOUR: I don't think he's ever even <i>heard</i> of Paradise, let alone any of their songs."
            
            NOUR: "Besides, hearing something on a record is always way better."
            
            MS. SUWAN: "That is true."
            
            NICK: "I've never heard anything on a record before! Can I hear it? Please?" // (EXCITED)

    - Ms. Suwan sighs and steps back.
    
    // MS SUWAN STEPS BACK SLIGHTLY FROM THE MUSIC STAND
    
    MS. SUWAN: "Fine. Go ahead."
    
    NOUR: "Time to put on a classic... <i>Sounds like Old Angst</i>!" 
    
    // NOUR PUTS THE RECORD INTO THE PLAYER AND STEPS BACK SLIGHTLY FROM THE MUSIC STAND TO JOIN MS SUWAN.
    
    The record enters the player, and the music begins to fill the store.
    
    //CODE HERE - PLAY "SOUNDS LIKE OLD ANGST" 
    
    NOUR: "So, your friends... Do you still talk to them?"
    
    // Ms.Suwan's smile fades a little. THIS SHOULD BE TOLD IN PORTRAIT
    
    MS. SUWAN: "Not anymore."
    
    NICK: "What happened?"
    
    NOUR: "Nick!" // (SHOCKED)
    
    NICK: "What? I wanna know!" // (ANGRY)

    MS. SUWAN: "Listening to music doubled the time it took me to complete my assignments."
    
    MS. SUWAN: "I struggled to catch up to my friends because I was still learning English at the time."

    MS. SUWAN: "Once, they got upset at me for not going to a concert with them."
    
    MS. SUWAN: "I chose not to fail my finals over the concert in the end."
    
    MS. SUWAN: "They didn't care about my situation, so I stopped caring about them."
    
    MS. SUWAN: "...I stopped hanging out with them, they started going to concerts without me..."
    
    MS. SUWAN: "...And the rest is history." // (SAD)
    
    NICK: "That really sucks." // (SAD)
    
    MS. SUWAN: "There is nothing I can do about it anymore."
    
    MS. SUWAN: "I haven't talked to them since that fight."
    
    NOUR: "Have you <i>thought</i> about them since that fight?"
    
    MS. SUWAN: "..."
    
    MS. SUWAN: "Can you turn that off? I am getting a migraine." // (ANGRY)
    
    NOUR: <i>She's avoiding all of her problems again.</i>
    
    NOUR: <i>Just when I thought we were getting somewhere...</i>
    
    NOUR: <i>If she could make amends with her old friends, maybe they'll help us get out of here!</i>

    MS. SUWAN: "Nour. Turn the music off. Now." // (ANGRY)
    
        * "Fine, I'll turn it off."
        
            //CODE HERE - STOP PLAYING "SOUNDS LIKE OLD ANGST" 

            MS. SUWAN: "Finally. Let's stop talking about my past — it isn't helping us get anywhere."
            
            MS. SUWAN: "Namely, out of here."
            
            NOUR: "You not talking about it is hurting us MORE."
            
            NOUR: "You won't talk to the people who might be able to help us get out of here!" // (ANGRY)
            
            MS. SUWAN: "They don't know anything. Leave it alone, Nour."
            
            MS. SUWAN: "We're leaving."
        
        * "..."
            
            MS. SUWAN: "...Nour. Did you hear what I just said?" // (ANGRY)
            
            //post beta, we can have this option perhaps
                ** "Yeah, and I'm choosing to ignore you!" // (ANGRY)
                
                    MS. SUWAN: "..." // (SHOCKED)
                
                    MS. SUWAN: "Nour, I am the <i>adult</i> here, and part of the <i>law</i>. You <i>will</i> listen to me." // (ANGRY)
                
                    NOUR: "Except you're the one acting immature right now!" // (ANGRY)
                
                ** "You want it louder?"
                    
                    //CODE HERE - MAKE MUSIC VOLUME LOUDER
                    
                    MS. SUWAN: "This is extremely immature of you." // (ANGRY)
                    
                    NOUR: "I can't hear you over this <i>great music</i> that's supposed to <i>bring people together</i>." // (ANGRY)
    
    -  &PORTRAIT_nour_nourangry
    
    NOUR: "You can't keep avoiding your friends, only to come here and get all sad about it!" // (ANGRY)
    
    MS. SUWAN: "Excuse me?" // (SHOCKED)
    
    NOUR: "You keep talking about how hard you worked for everything in your life."
    
    NOUR: "You haven't backed down from ANYTHING!" // (ANGRY)
    
    NOUR: "How could YOU, of all people, be afraid of a <i>conversation</i>?" // (ANGRY)
    
    MS. SUWAN: "You cannot understand what this is like."
    
    MS. SUWAN: "To look back at the past and know you cannot change a thing, because it is decades behind you."
    
    MS. SUWAN: "Sometimes the best thing you can do is forget about it and move on."
    
    &PORTRAIT_nour_noursad
    
    NOUR: "Did you though?"
    
    MS. SUWAN: "Did I <i>what</i>?"
    
    NOUR: "Did you forget about it? Did you move on?"
    
    MS. SUWAN: "..." // (SAD)
    
    NOUR: "Sometimes you need to confront your past."
    
    NOUR: "Even if you know there's nothing you can do to change it."
    
    NOUR: "I promise you, carrying this with you and bottling it up will only hurt you more."
    
    NOUR: "Hurt <i>us</i> more." // (SAD)
    
    MS. SUWAN: "I..." // (SAD)
    
        * "I'll be right by your side."
        
        * "If none of this is real, there's no risk involved, right?"
        
        * "Do it for yourself."
    
    - NOUR: "I think working out your feelings with them could help you and your past feelings."
    
    NOUR: "What do you say?"
    
    MS. SUWAN: "...Fine. I'll talk to them."
    
    NOUR: <i>I finally managed to convince her to confront them.</i>
    
    NOUR: <i>We should go find Chad, Brody, and Kristy again.</i>

    -> END

//figure out how to implement intervals for when other items are not collected
== musicstand1_repeat ==
    
    NOUR: <i>It'd be nice to get Ms. Suwan to re-accept her old hobby.</i>
    
    NOUR: <i>But I don't think she's quite ready yet.</i>
    
    NOUR: <i>I can come back to this later.</i>
    
    NOUR: <i>For now, we should go to Chad, Brody, and Kristy, before she changes her mind about talking to them.</i>
    
    -> END

== musicstand2 ==

    MS. SUWAN: "..."
    
    MS. SUWAN: "Why are we here again?" // (ANGRY)
    
    NOUR: "You know, I used to listen to Paradise with my ex-friend, too.</i>
    
    &PORTRAIT_nour_noursad
    
    NOUR: <i>Sometimes it hurts to listen to, which is why I hadn't played it in so long." // (SAD)
    
    MS. SUWAN: "..."
    
    MS. SUWAN: "Do you think it's possible? To still love the things that remind you of your mistakes?"
        
        * "You have to accept the pain either way."
        
            NOUR: "Accepting the pain of a memory also lets you remember the good things that remain."
            
            NOUR: "Like the happiness music gave you."
            
        * "Nothing should stop you from enjoying what you love."
        
            NOUR: "I think it'd be such a shame to not let yourself enjoy Paradise anymore because of what happened."
            
            NOUR: "Don't let your past stop you from finding joy in what you've always loved."
            
        * "We can't carry our regrets with us forever."
        
            NOUR: "Some things work out, and some don't. But it doesn't mean you've failed or that you're ruined forever."
            
            NOUR: "What matters is that you can free yourself of the guilt by acknowledging that you aren't perfect. It's ok to forgive yourself."
    
    - NOUR: "You can't change what happened, but that doesn't mean you should avoid it entirely."
    
    NOUR: "Plus, you have the opportunity to do that anyway."
    
    NOUR: "Thanks to this weird mall."
    
    NOUR: "I don't think any of this is real, the stakes are... relatively low, right?"
    
    MS. SUWAN: "I suppose you're right about that."
    
    NOUR: "...Why don't you take an album with you?"
    
    NOUR: "You're not a stressed out student anymore. You could rock out once in a while."
    
    MS. SUWAN: "...Alright. Which one should I take?"
    
    // NOUR STEPS FORWARD TOWARDS THE MUSIC STAND AND TAKES ONE OF THE ALBUMS, THEN TURNS BACK AROUND AND GOES TOWARDS MS SUWAN, STOPPING IN FRONT OF HER AND GIVING HER THE ALBUM.
    
        * [<i>Actually, I Do Mind</i>]
            Nour gives Ms. Suwan <i>Actually, I Do Mind</i>. 
            
        * [<i>Stain</i>]
            Nour gives Ms. Suwan <i>Stain</i>. 
            
        * [<i>Out Birth</i>]
            Nour gives Ms. Suwan <i>Out Birth</i>. 
            
// MS SUWAN REACHES OUT AND TAKES THE ALBUM.
    
    - MS. SUWAN: "Thank you, Nour."
    
    MS. SUWAN: "I want to take this album and go apologize to them all."
    
    MS. SUWAN: "I don't know if they'll accept my apology, but I have to try." // (SAD)
    
    NOUR: <i>It looks like Ms. Suwan is ready to apologize to Chad, Brody and Kristy.</i>
    
    NOUR: <i>We should go to them now.</i>
        
    &LIGHT_Suwan1
        
    -> light_on

== musicstand2_repeat ==

    NOUR: <i>Ms. Suwan's ready to apologize to Chad, Brody and Kristy. We should go to them now.</i>
    
    -> END

== musicstand4 ==
    NOUR: "I wish I could've gone to a Paradise concert. It must've been so cool to hear them live." // (SAD)
    
    MS. SUWAN: "It was. Nothing like the digital recordings—the energy isn't the same."
    
    MS. SUWAN: "I should've gone to more concerts." // (SAD)
        * "It's not too late."
        
            NOUR: If you discover newer rock artists, you could go to their concerts."
            
            NOUR: "And older bands do reunion concerts too. Whatever the future holds, I know there's a rock concert waiting for you to be in it."
            
        * "At least we still have these records."
        
            NOUR: Nothing like having vinyl to really get into the music."
            
            NOUR: "I bet you could even find live recordings, too."

    - MS. SUWAN: "I'll keep that in mind, then."
    
    MS. SUWAN: "Thank you, Nour."
    
        -> END
